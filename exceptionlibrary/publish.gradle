apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'com.github.NTonakanyan'
def LIB_ARTIFACT_ID = 'exception-library'
def LIB_VERSION = '0.5.1'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/NTonakanyan/Android-Exception-library")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USER")
                password = project.findProperty("gpr.key") ?: System.getenv("PERSONAL_ACCESS_TOKEN")
            }
        }
        maven {
            name = 'CustomMavenRepo'
            url = "file://${buildDir}/repo"
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId LIB_GROUP_ID
            artifactId LIB_ARTIFACT_ID
            version LIB_VERSION
            artifact("$buildDir/outputs/aar/exceptionlibrary-debug.aar")
            artifact(sourceJar)

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}